---
title: Shape data
---

TQL comes with numerous transformation
[operators](/references/operators) that change the shape of events.
TQL has [functions](/references/functions) that work on values within
a single event.

This guides showcases the operators and functions that you need for day-to-day
data shaping tasks.

## Use operators to reshape data flows

Operators work on streams of events and—unlike functions—can keep state between
multiple events. Here is a visual overview of transformations that you can
perform over a stream of events:

![Shaping Overview](shape-data.svg)

Let's dive right in.

### Filter events with `where`

Use [`where`](/references/operators/where) to filter events in the
input with an [expression](/references/language/expressions):

```tql
from {x: 1, y: "foo"},
     {x: 2, y: "bar"},
     {x: 3, y: "baz"}
where x != 2 and y.starts_with("b")
```

```tql
{x: 3, y: "baz"}
```

### Slice events with `head`, `tail`, and `slice`

Use the [`head`](/references/operators/head) and
[`tail`](/references/operators/tail) operators to get the first or
last N records of the input.

Get the first event:

```tql
from {x: 1, y: "foo"},
     {x: 2, y: "bar"},
     {x: 3, y: "baz"}
head 1
```

```tql
{x: 1, y: "foo"}
```

Get the last two events:

```tql
from {x: 1, y: "foo"},
     {x: 2, y: "bar"},
     {x: 3, y: "baz"}
tail 2
```

```tql
{x: 2, y: "bar"}
{x: 3, y: "baz"}
```
:::caution[`tail` is blocking]
The `tail` operator must wait for its entire input, whereas `head N` terminates
immediately after the first `N` records have arrived. Use `head` for the
majority of use cases and `tail` only when you have to.
:::

The [`slice`](/references/operators/slice) operator generalizes `head` and
`tail` by allowing for more flexible slicing. For example, to return every
other event starting from the third:

```tql
from {x: 1, y: "foo"},
     {x: 2, y: "bar"},
     {x: 3, y: "baz"},
     {x: 4, y: "qux"},
     {x: 5, y: "corge"},
     {x: 6, y: "grault"}
slice begin=3, stride=2
```

```tql
{x: 4, y: "qux"}
{x: 6, y: "grault"}
```

### Pick fields with `select` and `drop`

Use the [`select`](/references/operators/select) operator to pick
fields:

```tql
from {x: 1, y: "foo"},
     {x: 2, y: "bar"},
     {x: 3, y: "baz"}
select x
```

```tql
{x: 1}
{x: 2}
{x: 3}
```

The [`drop`](/references/operators/drop) operator is the dual to `select` and
removes the specified fields:

```tql
from {x: 1, y: "foo"},
     {x: 2, y: "bar"},
     {x: 3, y: "baz"}
drop x
```

```tql
{y: "foo"}
{y: "bar"}
{y: "baz"}
```

### Sample schemas with `taste`

The [`taste`](/references/operators/taste) operator provides a sample of the
first N events of every unique schemas. For example, to get 3 unique samples:

```tql
from {x: 1, y: "foo"},
     {x: 2, y: "bar"},
     {x: 1},
     {x: 2},
     {y: "foo"}
taste 1
```

```tql
{x: 1, y: "foo"}
{x: 1}
{y: "foo"}
```

### Invert event ordering with `reverse`

Use `reverse` to invert the order of a stream of events:

```tql
from {x: 1},
     {x: 2},
     {x: 3}
reverse
```

```tql
{x: 3}
{x: 2}
{x: 1}
```

### Add fields with `set` assignment

Use the [`set`](/references/operators/set) operator to add new fields
to the output.

```tql
from {x: 1},
     {x: 2}
set y = x + 1
```

```tql
{x: 1, y: 2}
{x: 2, y: 3}
```

You can assign a field name name and project at the same time with an assignment
in [`select`](/references/operators/select):

```tql
from {x: 1, y: "foo"},
     {x: 2, y: "bar"}
select y=x
```

```tql
{y: 1}
{y: 2}
```

### Relocate fields with `move`

Use the `move` operator to combine `set` and `drop`. This pipeline

```tql
from {old: 42}
move new = old
```

produces the same output as this one:

```tql
from {old: 42}
set new = old
drop old
```

```tql
{new: 42}
```

Moving multiple fields is also possible. The pipeline

```tql
from {foo: 1, bar: 2}
move foo=bar, qux=foo
```

is equivalent to

```tql
from {foo: 1, bar: 2}
set foo=bar, qux=foo
drop bar, foo
```

and produces:

```tql
{
  foo: 2,
  qux: 1,
}
```

However, it is not equivalent to the following pipeline containing single
assignments:

```tql
from {foo: 1, bar: 2}
set foo=bar
drop bar
set qux=foo
drop foo
```

which yields:

```tql
{
  qux: 2,
}
```

### Aggreate events with `summarize`

Use [`summarize`](/references/operators/summarize) to group and
aggregate data.

```tql
from {x: 0, y: 0, z: 1},
     {x: 1, y: 1, z: 2},
     {x: 1, y: 1, z: 3}
summarize y, x=sum(x)
```

```tql
{y: 0, x: 0}
{y: 1, x: 2}
```

A variety of [aggregation
functions](/references/functions#aggregation) make it possible to
combine grouped data.

### Reorder events with `sort`

Use [`sort`](/references/operators/sort) to arrange the output records
according to the order of a specific field.

```tql
from {x: 2, y: "bar"},
     {x: 3, y: "baz"},
     {x: 1, y: "foo"}
sort -x
```

```tql
{x: 3, y: "baz"}
{x: 2, y: "bar"}
{x: 1, y: "foo"}
```

Prepending the field with `-` reverses the sort order.

### Break up lists of records with `unroll`

Working with lists of can be cumbersome. To break up a list into a sequence of
events, use the [`unroll`](/references/operators/unroll) operator:

```tql
from {
  xs: [{a: 1}, {a: 2}],
  y: "foo",
}
unroll xs
```

```tql
{
  xs: {
    a: 1,
  },
  y: "foo",
}
{
  xs: {
    a: 2,
  },
  y: "foo",
}
```

Note that `unroll` produces as many events as there are elements in the
unrolled list. All other fields are simply copied over.

## Use functions to change single events

Functions work on single values, including the top-level event that you can
reference with the `this` keyword.

### Combine records with `merge`

Use the [`merge`](/references/functions/merge) function to combine two
records:

```tql
from {
  foo: {
    bar: 1,
    baz: 2,
  },
  qux: {
    fred: 3,
    george: 4,
    bar: 5,
  }
}
this = merge(foo, qux)
```

```tql
{
  bar: 5,
  baz: 2,
  fred: 3,
  george: 4
}
```

Note that the field `bar`, which is present in both record `foo` and `qux`, has
the value `5` in the output because *fields from the second argument to `merge`
overwrite fields from the first if they have the have the same name*.

Instead of writing

```tql
this = merge(foo, qux)
```
you can also write

```tql
this = {...foo, ...qux}
```

as an idiomatic shorthand. The spread expression `...` expands records in the
exact same was as `merge`.

### Combine lists with `concatenate`

Use [`concatenate`](/references/functions/concatenate) to add one list
add the end of another:

```tql
from {
  xs: [1,2,3],
  ys: [4,5,6],
}
select result = concatenate(xs, ys)'
```

```tql
{
  result: [
    1,
    2,
    3,
    4,
    5,
    6,
  ],
}
```

The spread expression `...` also works for lists. You can rewrite the above
example

```tql
select result = concatenate(xs, ys)'
```

more idiomatically as

```tql
select result = [...xs, ...ys]'
```

and get the same result.

### Add values to lists with `append` and `prepend`

To add a single value to a list, use
[`append`](/references/functions/append) to add it to the back and
[`prepend`](/references/functions/prepend) to add it to the front:

```tql
from {
  xs: [2],
}
xs = append(xs, 3)
xs = prepend(xs, 1)
```

```tql
{
  result: [
    1,
    2,
    3,
  ],
}
```

### Perform bitwise operations

TQL comes with bitwise functions on numeric data types:

```tql
from {
  band: bit_and(5, 3),
  bor: bit_or(5, 3),
  bxor: bit_xor(5, 3),
  bnot: bit_not(5),
  shl: shift_left(5, 2),
  shr: shift_right(5, 1),
}
```

```tql
{
  band: 1,  // (0101 & 0011 = 0001)
  bor: 7,   // (0101 | 0011 = 0111)
  bxor: 6,  // (0101 ^ 0011 = 0110)
  bnot: -6, // (~0101 = 1010)
  shl: 20,  // (0101 << 2 = 10100)
  shr: 2,   // (0101 >> 1 = 0010)
}
```

Note that integral types in Tenzir have 64 bits, and we're only showing the least significant bits for brevity.
