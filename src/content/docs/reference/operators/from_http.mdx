---
title: from_http
category: Inputs/Events
example: 'from_http "https://example.com/api"'
---

import Op from '@components/see-also/Op.astro';
import Guide from '@components/see-also/Guide.astro';
import Integration from '@components/see-also/Integration.astro';

Sends an HTTP/1.1 request and returns the response as events.

```tql
from_http url:string, [method=string, body=record|string|blob, encode=string,
          headers=record, error_field=field, paginate=string,
          paginate_delay=duration, connection_timeout=duration,
          max_retry_count=int, retry_delay=duration, tls=record]
          { … }
```

## Description

The `from_http` operator issues an HTTP request and returns the response as
events.

:::tip[Format and Compression Inference]

The `from_http` operator automatically infers the file format (such as JSON, CSV, Parquet, etc.) and compression type (such as gzip, zstd, etc.) directly from the URL's file extension, just like the generic `from` operator. This makes it easier to load data from HTTP sources without manually specifying the format or decompression step.

If the format or compression cannot be determined from the URL, the operator will fall back to using the HTTP `Content-Type` and `Content-Encoding` response headers to determine how to parse and decompress the data.

If neither the URL nor the HTTP headers provide enough information, you can explicitly specify the decompression and parsing steps using a pipeline argument.
:::

### `url: string`

URL to connect to.

import HTTPClientOptions from '@partials/operators/HTTPClientOptions.mdx';

<HTTPClientOptions />

### `error_field = field (optional)`

Field to insert the response body for HTTP error responses (status codes not in the 2xx or 3xx range).

When set, any HTTP response with a status code outside the 200–399 range will
have its body stored in this field as a `blob`. Otherwise, error responses are
skipped and an error is emitted.

import TLSOptions from '@partials/operators/TLSOptions.mdx';

<TLSOptions tls_default="false"/>

### `{ … } (optional)`

A pipeline that receives the response body as bytes, allowing parsing per
request. This is especially useful in scenarios where the response body can be
parsed into multiple events.

Inside the pipeline, the `$response` variable is available as a record with the
following fields:

| Field     | Type     | Description                           |
| :-------- | :------- | :------------------------------------ |
| `code`    | `uint64` | The HTTP status code of the response. |
| `headers` | `record` | The response headers.                 |

If not provided, the operator will attempt to infer the parsing operator from
the `Content-Type` header. Should this inference fail (e.g., unsupported or
missing `Content-Type`), the operator raises an error.

## Examples

### Make a GET request

Make a request to [urlscan.io](https://urlscan.io/docs/api#search) to search for
scans for `tenzir.com` and get the first result.

```tql
from_http "https://urlscan.io/api/v1/search?q=tenzir.com"
unroll results
head 1
```

```tql
{
  results: {
    submitter: { ... },
    task: { ... },
    stats: { ... },
    page: { ... },
    _id: "0196edb1-521e-761f-9d62-1ca4cfad5b30",
    _score: null,
    sort: [ "1747744570133", "\"0196edb1-521e-761f-9d62-1ca4cfad5b30\"" ],
    result: "https://urlscan.io/api/v1/result/0196edb1-521e-761f-9d62-1ca4cfad5b30/",
    screenshot: "https://urlscan.io/screenshots/0196edb1-521e-761f-9d62-1ca4cfad5b30.png",
  },
  total: 9,
  took: 296,
  has_more: false,
}
```

### Send a POST request with JSON body

```tql
from_http "https://httpbin.org/post", body={key: "value"}, encode="json" {
  read_json
}
```

### Access response metadata

Use the `$response` variable inside a parsing pipeline to access the HTTP
response code and headers:

```tql
from_http "https://example.com/api", method="put" {
  read_json
  where $response.code == 200
  response = $response
}
```

### Paginate via Link Headers

Use `paginate="link"` to automatically follow RFC 8288 `Link` headers with
`rel=next`:

```tql
from_http "https://api.github.com/repos/tenzir/tenzir/issues?per_page=10",
  paginate="link"
```

Many APIs (such as GitHub, GitLab, and Jira) use the `Link` header for
pagination. The operator extracts the `rel=next` URL from the header and
continues fetching until no more pages are available.

### Retry Failed Requests

Configure retries for failed requests:

```tql
from_http "https://api.example.com/data", max_retry_count=3, retry_delay=2s
```

This tries up to 3 times, waiting 2 seconds between each retry.

## See Also

- <Op>accept_http</Op>
- <Op>http</Op>
- <Op>serve</Op>
- <Guide>collecting/fetch-via-http-and-apis</Guide>
- <Guide>enrichment/enrich-with-threat-intel</Guide>
- <Guide>enrichment/work-with-lookup-tables</Guide>
- <Integration>http</Integration>
